apply plugin: 'com.android.application'
apply plugin: 'placeholder-resolver'

android {
    compileSdkVersion 29
    android.ndkVersion '21.3.6528147'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'green_green_avk.anotherterm'
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 95
        versionName "MkIIIv36"
        vectorDrawables.useSupportLibrary = true

        resConfigs "en"

        def SHELL_SESSION_TOKEN_VAR = "SHELL_SESSION_TOKEN"
        buildConfigField "String", "SHELL_SESSION_TOKEN_VAR", "\"$SHELL_SESSION_TOKEN_VAR\""

        externalNativeBuild {
            cmake {
                cppFlags "-DSHELL_SESSION_TOKEN_VAR=\\\"$SHELL_SESSION_TOKEN_VAR\\\""
                arguments "-DAPP_ID_BASE=\\\"${applicationId}\\\""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
        variant.resValue "string", "applicationId", variant.applicationId
        variant.buildConfigField "int", "TARGET_SDK_VERSION",
                "${variant.mergedFlavor.targetSdkVersion.apiLevel}"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            externalNativeBuild {
                cmake {
                    arguments "-DAPP_ID_TYPE=\\\"${applicationIdSuffix}\\\""
                }
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel 'FULL'
            externalNativeBuild {
                cmake {
                    arguments "-DAPP_ID_TYPE=\\\"\\\""
                }
            }
        }
    }

    flavorDimensions "compat"

    productFlavors {
        googleplay {
            // Purely for Google Play publishing
            // (to comply with possible future app-bundle requirement)
            dimension "compat"
            externalNativeBuild.cmake.arguments "-DAPP_ID_SDK=\\\"\\\""
        }
        redist {
            // Redistributable downgradable all-in-one apk
            dimension "compat"
            versionCode 90 // Fixed to enable downgradability
            applicationIdSuffix ".redist"
            externalNativeBuild.cmake.arguments "-DAPP_ID_SDK=\\\"${applicationIdSuffix}\\\""
        }
        oldgood {
            // Redistributable downgradable all-in-one apk with target API 28
            dimension "compat"
            versionCode 90 // Fixed to enable downgradability
            applicationIdSuffix ".oldgood"
            targetSdkVersion 28
            externalNativeBuild.cmake.arguments "-DAPP_ID_SDK=\\\"${applicationIdSuffix}\\\""
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation project(':MaterialSeekBarPreference')
    implementation project(':PtyProcess')
    implementation 'com.github.felHR85:UsbSerial:6.1.0'
    implementation 'com.jcraft:jsch:0.1.55'
    implementation project(':TelnetClient')
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.github.green-green-avk:libusbmanager:1.4'
    implementation 'com.github.green-green-avk:anothertermshellpluginutils:1.13'
    implementation project(path: ':bluetoothspp')
    implementation 'me.saket:better-link-movement-method:2.2.0'
}

repositories {
    mavenCentral()
}
