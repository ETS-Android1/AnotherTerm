apply plugin: 'com.android.application'
apply plugin: 'placeholder-resolver'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'green_green_avk.anotherterm'
        minSdkVersion 14
        /*
         * execve() kernel call for any user writable location is blocked for user applications
         * since API 29 as a part of w^x policy enforcement in order to "increase security"...
         * Android becomes hostile to its users... :(
         *
         * execve() is essential for the Local Terminal functionality of this application
         * (and for many other applications too).
         *
         * Possibly, I'll put PRoot inside to mitigate it as long as it's still possible
         * to set the memory page execution flag.
         *
         * So, left it targeted to API 28 for now till the decision will be made.
         */
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 76
        versionName "MkIIIv18-dev"
        vectorDrawables.useSupportLibrary = true

        resConfigs "en"

        def SHELL_SESSION_TOKEN_VAR = "SHELL_SESSION_TOKEN"
        buildConfigField "String", "SHELL_SESSION_TOKEN_VAR", "\"$SHELL_SESSION_TOKEN_VAR\""

        externalNativeBuild {
            cmake {
                cppFlags "-DSHELL_SESSION_TOKEN_VAR=\\\"$SHELL_SESSION_TOKEN_VAR\\\""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
        variant.resValue "string", "applicationId", variant.applicationId
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            externalNativeBuild {
                cmake {
                    cppFlags "-DAPPLICATION_ID=\\\"${android.defaultConfig.applicationId}${applicationIdSuffix}\\\""
                }
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags "-DAPPLICATION_ID=\\\"${android.defaultConfig.applicationId}\\\""
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    //noinspection GradleDependency // Dalvik in Android 4 can't newer versions...
    implementation 'commons-io:commons-io:2.6'
    //noinspection GradleDependency // Dalvik in Android 4 can't newer versions...
    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation project(':MaterialSeekBarPreference')
    implementation project(':PtyProcess')
    implementation project(':usbserial')
    implementation 'com.jcraft:jsch:0.1.55'
    implementation project(':TelnetClient')
    //noinspection GradleDependency // Dalvik in Android 4 can't newer versions...
    implementation 'org.apache.commons:commons-collections4:4.2'
    implementation 'com.github.green-green-avk:LibUsbManager:1.2'
    implementation 'com.github.green-green-avk:anothertermshellpluginutils:1.8'
    implementation project(path: ':bluetoothspp')
    implementation 'me.saket:better-link-movement-method:2.2.0'
}

repositories {
    mavenCentral()
}
